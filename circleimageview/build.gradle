apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// This is the library version used when deploying the artifact
version = "1.2.1"

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'
    resourcePrefix "circleimageview__"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 7
        versionName version
    }
    buildTypes {
    }
}

def siteUrl = 'https://github.com/jarrodholliday/CircleImageView'
def gitUrl = 'https://github.com/jarrodholliday/CircleImageView.git'
group = "com.github.jarrodholliday"

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "CircleImageView"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                name 'CircleImageView is a component which can display circular image'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'IndigoSAN'
                        name 'Aliaksandr Zhukovich'
                        email 'zhukovich.a@gmail.com'
                    }
                    developer {
                        id 'jarrodholliday'
                        name 'Jarrod Holliday'
                        email 'jarrodholliday@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.nineoldandroids:library:2.4.0'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}


